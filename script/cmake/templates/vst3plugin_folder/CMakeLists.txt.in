cmake_minimum_required(VERSION 3.14.0)

option(SMTG_ENABLE_VST3_PLUGIN_EXAMPLES "Enable VST 3 Plug-in Examples" OFF)
option(SMTG_ENABLE_VST3_HOSTING_EXAMPLES "Enable VST 3 Hosting Examples" OFF)

set(CMAKE_OSX_DEPLOYMENT_TARGET @SMTG_MACOS_DEPLOYMENT_TARGET@ CACHE STRING "")

set(vst3sdk_SOURCE_DIR "@SMTG_VST3_SDK_SOURCE_DIR@")
if(NOT vst3sdk_SOURCE_DIR)
    message(FATAL_ERROR "Path to VST3 SDK is empty!")
endif()

project(@SMTG_CMAKE_PROJECT_NAME@
    # This is your plug-in version number. Change it here only.
    # Version number symbols usable in C++ can be found in
    # source/version.h and ${PROJECT_BINARY_DIR}/projectversion.h.
    VERSION 1.0.0.0 
    DESCRIPTION "@SMTG_CMAKE_PROJECT_NAME@ VST 3 Plug-in"
)

set(SMTG_VSTGUI_ROOT "${vst3sdk_SOURCE_DIR}")

add_subdirectory(${vst3sdk_SOURCE_DIR} ${PROJECT_BINARY_DIR}/vst3sdk)
smtg_enable_vst3_sdk()

smtg_add_vst3plugin(@SMTG_CMAKE_PROJECT_NAME@
    source/version.h
    source/@SMTG_PREFIX_FOR_FILENAMES@cids.h
    source/@SMTG_PREFIX_FOR_FILENAMES@processor.h
    source/@SMTG_PREFIX_FOR_FILENAMES@processor.cpp
    source/@SMTG_PREFIX_FOR_FILENAMES@controller.h
    source/@SMTG_PREFIX_FOR_FILENAMES@controller.cpp
    source/@SMTG_PREFIX_FOR_FILENAMES@entry.cpp
)

#- VSTGUI Wanted ----
if(SMTG_ENABLE_VSTGUI_SUPPORT)
    target_sources(@SMTG_CMAKE_PROJECT_NAME@
        PRIVATE
            resource/@SMTG_PREFIX_FOR_FILENAMES@editor.uidesc
    )
    target_link_libraries(@SMTG_CMAKE_PROJECT_NAME@
        PRIVATE
            vstgui_support
    )
    smtg_target_add_plugin_resources(@SMTG_CMAKE_PROJECT_NAME@
        RESOURCES
            "resource/@SMTG_PREFIX_FOR_FILENAMES@editor.uidesc"
    )
endif(SMTG_ENABLE_VSTGUI_SUPPORT)
# -------------------

smtg_target_add_plugin_snapshots (@SMTG_CMAKE_PROJECT_NAME@
    RESOURCES
        resource/@SMTG_Processor_PLAIN_UUID@_snapshot.png
        resource/@SMTG_Processor_PLAIN_UUID@_snapshot_2.0x.png
)

target_link_libraries(@SMTG_CMAKE_PROJECT_NAME@
    PRIVATE
        sdk
)

smtg_target_configure_version_file(@SMTG_CMAKE_PROJECT_NAME@)

if(SMTG_MAC)
    smtg_target_set_bundle(@SMTG_CMAKE_PROJECT_NAME@
        BUNDLE_IDENTIFIER @SMTG_PLUGIN_IDENTIFIER@
        COMPANY_NAME "@SMTG_VENDOR_NAME@"
    )
    smtg_target_set_debug_executable(@SMTG_CMAKE_PROJECT_NAME@
        "/Applications/VST3PluginTestHost.app"
        "--pluginfolder;$(BUILT_PRODUCTS_DIR)"
    )
elseif(SMTG_WIN)
    target_sources(@SMTG_CMAKE_PROJECT_NAME@ PRIVATE 
        resource/win32resource.rc
    )
    if(MSVC)
        set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT @SMTG_CMAKE_PROJECT_NAME@)

        smtg_target_set_debug_executable(@SMTG_CMAKE_PROJECT_NAME@
            "$(ProgramW6432)/Steinberg/VST3PluginTestHost/VST3PluginTestHost.exe"
            "--pluginfolder \"$(OutDir)/\""
        )
    endif()
endif(SMTG_MAC)
